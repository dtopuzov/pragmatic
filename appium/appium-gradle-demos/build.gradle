plugins {
    id 'java'
    id 'checkstyle'
}

group 'org.example'
version '1.0-SNAPSHOT'

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.compilerArgs += '-parameters'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.0'
    implementation group: 'io.appium', name: 'java-client', version: '7.4.1'
    implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '4.2.2'
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.3'
    implementation group: 'org.awaitility', name: 'awaitility', version: '4.0.3'
}

test {
    useJUnitPlatform()

    systemProperties = [
            'config': System.getProperty("config")
    ]

    reports {
        junitXml.enabled = true
        html.enabled = true
    }

    testLogging {
        //noinspection GrUnresolvedAccess
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams = true
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}